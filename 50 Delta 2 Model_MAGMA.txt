S:=CuspidalSubspace(ModularForms(Gamma1(50),2));
f1:=S.1 - S.6 - 3*S.11 + S.16 + 2*S.21 + 4*S.26 + 2*S.31 - 2*S.36 - 3*S.41 - 6*S.46;
f2:=S.2 - 2*S.7 - S.12 + 3*S.17 - 3*S.22 + 5*S.27 + S.32 - 2*S.37 + 2*S.42 - 12*S.47;
f3:=S.3 - S.8 - 4*S.13 + 2*S.18 + 6*S.23 + 2*S.28 - 3*S.33 - 5*S.38 - 4*S.43 + S.48;
f4:=S.4 - 2*S.9 - 2*S.14 + 5*S.19 - S.24 + 3*S.34 - 4*S.39 - 3*S.44;
prec:=100;
L<q> := LaurentSeriesRing(Rationals(),prec);
R<x,y,z,w>:=PolynomialRing(Rationals(),4);
Bexp:=[L!qExpansion(f1,prec), L!qExpansion(f2,prec), L!qExpansion(f3,prec), L!qExpansion(f4,prec)];
	eqns:=[R | ];
Bexp;
	d:=1;
tf:=false;
	while tf eq false do
		d:=d+1;
		mons:=MonomialsOfDegree(R,d);
		monsq:=[Evaluate(mon,Bexp) : mon in mons];
		V:=VectorSpace(Rationals(),#mons);
		W:=VectorSpace(Rationals(),prec-10);
		h:=hom<V->W | [W![Coefficient(monsq[i],j) : j in [1..(prec-10)]] : i in [1..#mons]]>;
		K:=Kernel(h);
		eqns:=eqns cat [ &+[Eltseq(V!k)[j]*mons[j] : j in [1..#mons] ] : k in Basis(K)  ];
		X:=Scheme(ProjectiveSpace(R),eqns);
		if Dimension(X) eq 1 then
			if IsSingular(X) eq false then
				X:=Curve(ProjectiveSpace(R),eqns);
				if Genus(X) eq 4 then
					tf:=true;
				end if;
			end if;
		end if;
	end while;
eqns:=GroebnerBasis(ideal<R | eqns>); // Simplifying the equations.
	tf:=true;
	repeat
		t:=#eqns;
		tf:=(eqns[t] in ideal<R | eqns[1..(t-1)]>);
		if tf then 
			Exclude(~eqns,eqns[t]);
		end if;
	until tf eq false;
	t:=0;
	repeat
		t:=t+1;
		tf:=(eqns[t] in ideal<R | Exclude(eqns,eqns[t])>);	
		if tf then
			Exclude(~eqns,eqns[t]);
			t:=0;
		end if;
        until tf eq false;
t:=0;
	repeat
		t:=t+1;
		tf:=(eqns[t] in ideal<R | Exclude(eqns,eqns[t])>);	
		if tf then
			Exclude(~eqns,eqns[t]);
			t:=0;
		end if;
	until tf eq false and t eq #eqns;
X:=Curve(ProjectiveSpace(R),eqns);
X;